javaProject()
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: "jacoco"
checkstyle {
    toolVersion "8.8"
    ignoreFailures = false
    configFile file("config/checkstyle/checkstyle.xml")
}
checkstyleMain {
    source = 'src/main/java'
}
checkstyleTest {
    source = 'src/test/java'
}
tasks.withType(Checkstyle) {
    reports {
        xml.enabled = false
        html.enabled = true
        html.stylesheet resources.text.fromFile('config/xsl/checkstyle-custom.xsl')
    }
}
pmd {
    toolVersion = '6.0.0'
    ignoreFailures = false
    targetJdk = '1.7'
    sourceSets = [sourceSets.main]
    reportsDir = file("${project.buildDir}/reports/pmd")
    ruleSetFiles = files("/config/pmd/pmd-ruleset.xml")
}
findbugs {
    ignoreFailures = false
    toolVersion = "3.0.1"
    sourceSets = [sourceSets.main]
    excludeFilter = file("config/findbugs/excludeFilter.xml")
    reportsDir = file("$project.buildDir/reports/findbugs")
    effort = "max"
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
        html.stylesheet resources.text.fromFile('config/xsl/findbugs-custom.xsl')
    }
}
jacoco {
    toolVersion = '0.7.9'
    reportsDir = file("${buildDir}/reports/jacoco/customJacocoReportDir")
}
jacocoTestReport {
    reports {
        csv.enabled = false
        xml.enabled = true
        html.destination file("${buildDir}/reports/jacoco/jacocoHtml")
    }
}
test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/reports/jacoco/jacocoTest.exec")
    }
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.3
            }
        }
    }
}
check.dependsOn jacocoTestCoverageVerification
test.finalizedBy jacocoTestReport

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.13.0')
    }
}

apply plugin: 'com.palantir.docker'
apply plugin: "com.palantir.docker-run"

docker {
    name "${project.group}/${jar.baseName}:${version}"
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
}

dockerRun {
    name "${jar.baseName}"
    image "${project.group}/${jar.baseName}:${version}"
    command 'java', '-Djava.security.egd=file:/dev/./urandom', '-jar', 'app.jar'
    ports '8080'
}


dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('com.github.docker-java:docker-java:3.0.13')
}
